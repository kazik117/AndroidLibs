plugins {
    id("com.android.library")
    id("maven-publish")
}

project.ext {
    libVersion = '1.2.3.2'
    libVersionCode = 7
    libGroupId = 'com.xiaofeng.android'
    libArtifactId = 'flowlayoutmanager'
    gitUrl = 'git@github.com:xiaofeng-han/AndroidLibs.git'
    siteUrl = 'https://github.com/xiaofeng-han/AndroidLibs'
}

group = 'com.xiaofeng.android'
version = libVersion

android {
    compileSdkVersion androidCompileSdkVersion
    resourcePrefix "xiaofeng__"
    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode libVersionCode
        versionName libVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        unitTests.returnDefaultValues = false;
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation("androidx.appcompat:appcompat:$appCompatLibVersion")
    implementation("androidx.recyclerview:recyclerview:$recyclerLibVersion")

    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publishing {
    publications {
        maven(MavenPublication) {
            // This generates POM.xml with proper parameters
            pom {
                packaging = 'aar'

                // Add your description here
                description = 'FlowLayoutManager for android RecyclerView'
                url = siteUrl

                // Set your license
                licenses {
                    license {
                        name = 'GPL-v3.0'
                        url = 'http://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'xiaofeng-han'
                        name = 'Xiaofeng Han'
                        email = 'hhhanxf@gmail.com'
                    }
                }
                scm {
                    connection = gitUrl
                    developerConnection = gitUrl
                    url = gitUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


artifacts {
    archives sourcesJar
    archives javadocJar
}
